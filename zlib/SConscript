# -*- mode:python -*-
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
# 
# Copyright (c) Dimitry Kloper <kloper@users.sf.net> 2002-2012
# 
# This file is a part of the Scooter project. 
# 
# zlib/SConscript -- construction file for zlib
#

import os 
import string

Import('env', 'version')

env = env.Clone()

env.Append(CPPPATH = ['#blib/include'])
env.Append(CPPDEFINES = [ 'ZLIB_DLL' ])

env.Append(CFLAGS = ['-mthreads'])

env.Append(LIBPATH = ['#blib/lib'])
env.Append(LDFLAGS = ['-mthreads', '-mconsole'])

variant = env.subst('$VARIANT')
env.VariantDir(variant, '../ext/zlib')


srclist = map( lambda fname: os.path.join(variant,fname), 
               [ 'adler32.c',
                 'compress.c',
                 'crc32.c',
                 'deflate.c',
                 'gzclose.c',
                 'gzlib.c',
                 'gzread.c',
                 'gzwrite.c',
                 'infback.c',
                 'inffast.c',
                 'inflate.c',
                 'inftrees.c',
                 'trees.c',
                 'uncompr.c',
                 'zutil.c' ] )

inclist =  map( lambda fname: os.path.join(variant,fname), 
               [ 'zconf.h', 'zlib.h' ] )

zlib_name = env.subst('${CRSHPREFIX}zlib')

zlib = env.SharedLibrary(
    target = os.path.join(variant,zlib_name), 
    source = srclist
)

env.Install('#blib/bin', zlib[0])
env.Install('#blib/lib', zlib[1])
env.Install('#blib/include', inclist)

#
# SConscript -- end of file
#

